PREFIX rdf:		<http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX dc:		<http://purl.org/dc/elements/1.1/>
PREFIX dcterms:	<http://purl.org/dc/terms/>
PREFIX prov:	<http://www.w3.org/ns/prov#>
PREFIX calli:	<http://callimachusproject.org/rdf/2009/framework#>
PREFIX mecomp:	<http://rdf.myexperiment.org/ontologies/components/>
PREFIX df:		<http://www.swows.org/2012/06/dataflow#>

CONSTRUCT {
    ?dfRoot
    	df:namedInput
			[	a df:GraphNamer;
				df:name ?graphName;
				df:input ?sinkInput ]
}
WHERE {
	SERVICE ?dfEndpoint {
 		?dfRoot	mecomp:has-component ?namedSink.
		?namedSink
			a mecomp:Sink;
			dcterms:identifier ?sinkName.
		?sinkInput
			a mecomp:Input;
			mecomp:for-component ?namedSink.
 	}
	BIND(IRI( CONCAT( STR(?dfRoot), '#', ?sinkName ) ) AS ?graphName).
}