PREFIX rdf:			<http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX dc:			<http://purl.org/dc/elements/1.1/>
PREFIX dcterms:		<http://purl.org/dc/terms/>
PREFIX prov:		<http://www.w3.org/ns/prov#>
PREFIX calli:		<http://callimachusproject.org/rdf/2009/framework#>
PREFIX mecomp:		<http://rdf.myexperiment.org/ontologies/components/>
PREFIX swowscomp:	<http://swows.org/2014/09/components#>
PREFIX df:			<http://www.swows.org/2012/06/dataflow#>

CONSTRUCT {
	?dfRoot df:hasComponent ?dsProducer.
    ?dsProducer a df:DatasetProducer.
    ?defaultOutput
    	a df:DefaultGraphSelector;
		df:datasetProducer ?dsProducer.
    ?namedOutput
    	a df:NamedGraphSelector;
		df:name ?graphName;
		df:datasetProducer ?dsProducer.
}
WHERE {
	SERVICE ?dfEndpoint {
 		?dfRoot	mecomp:has-component ?dsProducer.
 		?dsProducer a ?dsProducerClass.
 		FILTER(?dsProducerClass IN
 					(	swowscomp:TransformProcessor,
 						swowscomp:Store,
 						swowscomp:DataflowProcessor ) ).
		OPTIONAL {
			?namedOutput
				a mecomp:Output;
				mecomp:for-component ?dsProducer;
				dcterms:identifier ?outputName.
		}
		OPTIONAL {
			?defaultOutput
				a mecomp:Output;
				mecomp:for-component ?dsProducer.
			FILTER NOT EXISTS {
				?defaultOutput dcterms:identifier ?anyName }
		}
 	}
	BIND(IRI( CONCAT( STR(?dsProducer), '#', ?outputName ) ) AS ?graphName).
}